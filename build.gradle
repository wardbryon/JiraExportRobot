import java.util.regex.Matcher

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'maven-publish'
}

group "$groupId"
version "$version"
sourceCompatibility = '17'

repositories {
    mavenCentral()
    maven { url "https://packages.atlassian.com/maven-public/" }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("$repoUrl")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = "$groupId"
            artifactId = "jira_exportbot"
            version = "$version"

            artifact bootJar
        }
    }
}

bootJar {
    archiveBaseName = "jira-export-robot-app"
    archiveVersion =  "$version-FINAL"
}


tasks.withType(GenerateMavenPom).all {
    doLast {
        File file = new File("$buildDir/publications/gpr/pom-default.xml")
        def text = file.text
        def pattern = "(?s)(<dependencyManagement>.+?<dependencies>)(.+?)(</dependencies>.+?</dependencyManagement>)"
        Matcher matcher = text =~ pattern
        if (matcher.find()) {
            text = text.replaceFirst(pattern, "")
            def firstDeps = matcher.group(2)
            text = text.replaceFirst(pattern, '$1$2' + firstDeps + '$3')
        }
        file.write(text)
    }
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'com.atlassian.jira:jira-rest-java-client-core:5.2.7'
    implementation 'com.atlassian.jira:jira-rest-java-client-api:5.2.7'
    implementation 'io.atlassian.fugue:fugue:4.7.2'
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api:2.1.6'
    implementation 'org.glassfish.jersey.core:jersey-client:2.35'
    implementation 'org.glassfish.jersey.core:jersey-common:2.35'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:2.35'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:2.35'
}

tasks.named('test') {
    useJUnitPlatform()
}